table _Medidas
	lineageTag: 5d2fdeda-57d0-4fb6-a1b9-209cd3522441

	measure 'Qtde Vendas' = DISTINCTCOUNT(fVendas[Num Venda])
		formatString: #,0
		displayFolder: _Principais
		lineageTag: c6e2dfed-b1f9-4897-983b-d5594124b063

		changedProperty = FormatString

	measure 'Positivação Clientes' = ```
			
			// Refere-se a Qtde de clientes que efetuaram compras 
			        DISTINCTCOUNT(fVendas[Id Cliente])
			
			
			
			
			```
		formatString: 0
		displayFolder: _Principais
		lineageTag: a8cec11c-cf82-487e-b973-012198c81a2d

	measure 'Positivação Produtos' = DISTINCTCOUNT(fVendas[Id Produto])
		formatString: 0
		displayFolder: _Principais
		lineageTag: c2012781-389d-45e0-893e-ae783dfc4007

	measure Faturamento = ```
			
			    CALCULATE(
			        SUMX(
			            fVendas,
			            fVendas[Qtde] * fVendas[Valor Unit]
			        ),
			        dStatus[Id Status] = 1
			    )
			```
		formatString: "R$"\ #,0;-"R$"\ #,0;"R$"\ #,0
		displayFolder: _Principais
		lineageTag: e65bb807-44fc-4cbc-983f-0ebaf546d84f

		changedProperty = FormatString

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure Custo = ```
			
			    CALCULATE(
			        SUMX(
			            fVendas,
			            fVendas[Qtde] * fVendas[Custo Unit]
			        ),
			        dStatus[Id Status] = 1 
			    )
			```
		formatString: "R$"\ #,0.00;-"R$"\ #,0.00;"R$"\ #,0.00
		displayFolder: _Principais
		lineageTag: 84d6ec66-1349-4b83-be22-0e27dc277249

		changedProperty = FormatString

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure Despesa = ```
			
			    CALCULATE(
			        SUMX(
			            fVendas,
			            fVendas[Qtde] * fVendas[Despesa Unit]
			        ),
			        dStatus[Id Status] = 1 
			    )
			```
		formatString: "R$"\ #,0;-"R$"\ #,0;"R$"\ #,0
		displayFolder: _Principais
		lineageTag: c53f0e5b-284f-461b-a91d-e160c2cf2d72

		changedProperty = FormatString

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure Impostos = ```
			
			    CALCULATE(
			        SUMX(
			            fVendas,
			            fVendas[Qtde] * fVendas[Impostos Unit]
			        ),
			        dStatus[Id Status] = 1 
			    )
			```
		formatString: "R$"\ #,0.00;-"R$"\ #,0.00;"R$"\ #,0.00
		displayFolder: _Principais
		lineageTag: ac65b685-7f01-4b6e-af8b-5a1abfac4c7f

		changedProperty = FormatString

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure Comissao = ```
			
			    CALCULATE(
			           SUMX(
			            fVendas,
			            fVendas[Qtde] * fVendas[Comissão Unit]
			        ),
			        dStatus[Id Status] = 1 
			    )
			```
		formatString: "R$"\ #,0;-"R$"\ #,0;"R$"\ #,0
		displayFolder: _Principais
		lineageTag: e2a5d29f-47dc-4b3f-8806-6aed1cffa86a

		changedProperty = FormatString

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure Abatimento = ```
			
			    [Custo] + [Despesa] + [Impostos] + [Comissao]
			```
		formatString: "R$"\ #,0.00;-"R$"\ #,0.00;"R$"\ #,0.00
		displayFolder: _Principais
		lineageTag: ffded7eb-d67b-4fda-b5ef-fcc413ba4754

		changedProperty = FormatString

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure Resultado = ```
			
			    [Faturamento] - [Abatimento]
			```
		formatString: "R$"\ #,0;-"R$"\ #,0;"R$"\ #,0
		displayFolder: _Principais
		lineageTag: 1578b7a7-9512-464d-9cf7-eba4bfc015c7

		changedProperty = FormatString

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure 'Margem Percentual' = ```
			
			
			    DIVIDE([Resultado],[Faturamento])
			
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: _Principais
		lineageTag: f7c93f03-2660-47dc-a1ba-3ab292788714

		changedProperty = FormatString

	measure Meta = SUM(fMetas[Valor Meta])
		formatString: "R$"\ #,0;-"R$"\ #,0;"R$"\ #,0
		displayFolder: _Principais
		lineageTag: 6ccbb42e-fabc-476a-b8ab-1da1af42d907

		changedProperty = FormatString

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure 'Porcentagem Meta' = ```
			DIVIDE([Faturamento],[Meta],0)
			
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: _Principais
		lineageTag: a06c30a5-1c72-4558-b5a4-c458ca60113c

		changedProperty = FormatString

	measure 'Circulo Farol' = UNICHAR(11044)
		displayFolder: Tooltips
		lineageTag: df64e01b-3113-48e7-9bb7-35ae25843680

	measure 'Faturamento Acumulado' = ```
			
			    CALCULATE(
			        [Faturamento],
			        CALCULATETABLE(
			            DATESYTD(dCalendario[Id Data]),
			            dCalendario[Datas com Venda] = TRUE()
			        )
			    )
			```
		displayFolder: _Temporais
		lineageTag: 3e353a4d-89d7-48d9-89c2-2390d17f76ab

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Faturamento LY' = ```
			
			
			CALCULATE(
			    [Faturamento],
			    CALCULATETABLE(
			        SAMEPERIODLASTYEAR( dCalendario[Id Data] ),
			        dCalendario[Datas com Venda] = TRUE() 
			    )
			)
			```
		formatString: "R$"\ #,0.00;-"R$"\ #,0.00;"R$"\ #,0.00
		displayFolder: _Temporais
		lineageTag: adc6447c-2e95-48a3-abe7-4a797e257a94

		changedProperty = FormatString

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure 'Faturamento YoY %' = ```
			
			
			VAR vAtual = [Faturamento] 
			VAR vAnterior = [Faturamento LY]
			
			VAR vResultado = 
			    DIVIDE(
			        vAtual - vAnterior,
			        vAnterior
			    ) 
			
			RETURN
			    vResultado 
			```
		displayFolder: _Temporais
		lineageTag: 3f4b893d-adee-4640-8752-e155e6e8519a

		changedProperty = FormatString

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Margem LY' = ```
			
			CALCULATE(
			    [Margem Percentual],
			    CALCULATETABLE(
			        SAMEPERIODLASTYEAR( dCalendario[Id Data] ),
			        dCalendario[Datas com Venda] = TRUE() 
			    )
			)
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: _Temporais
		lineageTag: 52bcce5b-b2f7-4f68-8f1a-ec461ddf5c1e

		changedProperty = FormatString

	measure 'Margem YoY %' = ```
			
			VAR vAtual = [Margem Percentual] 
			VAR vAnterior = [Margem LY]
			
			VAR vResultado = 
			    DIVIDE(
			        vAtual - vAnterior,
			        vAnterior
			    ) 
			
			RETURN
			    vResultado 
			```
		displayFolder: _Temporais
		lineageTag: 25be8cc5-3e67-4582-bf97-5a733a622ae2

		changedProperty = FormatString

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Resultado LY' = ```
			
			CALCULATE(
			    [Resultado],
			    CALCULATETABLE(
			        SAMEPERIODLASTYEAR( dCalendario[Id Data] ),
			        dCalendario[Datas com Venda] = TRUE() 
			    )
			)
			```
		formatString: "R$"\ #,0.00;-"R$"\ #,0.00;"R$"\ #,0.00
		displayFolder: _Temporais
		lineageTag: 44f36d9f-2ffd-48ca-9f8e-27d81909ce70

		changedProperty = FormatString

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure 'Resultado YoY %' = ```
			
			VAR vAtual = [Resultado] 
			VAR vAnterior = [Resultado LY]
			
			VAR vResultado = 
			    DIVIDE(
			        vAtual - vAnterior,
			        vAnterior
			    ) 
			
			RETURN
			    vResultado 
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: _Temporais
		lineageTag: 6499ef05-8be9-453e-82ad-c3afdb8ceac6

		changedProperty = FormatString

	measure 'Meta Cor' = ```
			
			
			Var vPorcentagem = [Porcentagem Meta] 
			Return
			
			    SWITCH(
			        TRUE(),
			        vPorcentagem >= 1, "#53A365",
			        vPorcentagem >= 0.8, "#E1A55D",
			        "#CF6F45"
			    )
			```
		displayFolder: _Principais
		lineageTag: 653c6938-ab0e-4235-a2bc-4cdabcddcf60

	measure 'Maior Valor' = ```
			
			
			
			VAR vTabela = 
			    FILTER(
			        ALLSELECTED( dCalendario[Mes Abreviado], dCalendario[Mês]), 
			        not ISBLANK( [Faturamento] )
			    )
			
			VAR vMaior_valor = 
			    MAXX( vTabela, [Faturamento] ) 
			
			VAR vCheck = 
			    IF( [Faturamento] = vMaior_valor, "s" ) 
			
			RETURN
			    vCheck  
			
			```
		displayFolder: Bônus 5.9 Deneb
		lineageTag: 212667bb-c97e-4ca6-9b66-ab5def382946

	measure 'Menor Valor' = ```
			
			
			
			VAR vTabela = 
			    FILTER(
			        ALLSELECTED( dCalendario[Mes Abreviado], dCalendario[Mês]), 
			        not ISBLANK( [Faturamento] )
			    )
			
			VAR vMenor_valor = 
			    MINX( vTabela, [Faturamento] ) 
			
			VAR vCheck = 
			    IF( [Faturamento] = vMenor_valor, "s" ) 
			
			RETURN
			    vCheck  
			
			```
		displayFolder: Bônus 5.9 Deneb
		lineageTag: 60ce1657-bb26-41b3-8b3c-4b90a476fedf

	measure 'Ultimo mes ?' = ```
			
			
			VAR vTabela =
			    ALLSELECTED( dCalendario[Mes Abreviado], dCalendario[Mês] )
			
			VAR vUltimo_mes = 
			    CALCULATE(
			        MONTH( MAX( fVendas[Data Pedido] ) ),
			        vTabela
			    )
			
			VAR vUltimo_mes_v2 = 
			    MAXX(
			        FILTER( vTabela, NOT ISBLANK( [Faturamento] ) ), 
			        dCalendario[Mês]
			    )
			
			VAR vMes_contexto = 
			    MAX( dCalendario[Mês] ) 
			
			VAR vResultado = 
			    IF( vMes_contexto = vUltimo_mes, "s" )  
			
			RETURN 
			    vResultado 
			
			```
		displayFolder: Bônus 5.9 Deneb
		lineageTag: 904540c3-28dd-4a8f-b27b-8ace78a84fef

	measure 'Faturamento Fixo' = ```
			
			
			CALCULATE(
			    [Faturamento], 
			    ALL( dVendedores[Vendedor], dVendedores[URL Foto] ) 
			)
			```
		formatString: #,0.00
		displayFolder: Bônus 5.9 Deneb\Gráfico Rosca
		lineageTag: d4cf9a8d-bdd4-426b-b146-cc187df998e8

	measure 'Margem Contribuição' = ```
			
			    DIVIDE( [Faturamento], [Faturamento Fixo] ) 
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Bônus 5.9 Deneb\Gráfico Rosca
		lineageTag: dbe542e9-c732-494b-b502-9a68c3ad0644

	measure 'Faturamento ACC' = ```
			
			
			VAR vVendedor_contexto = 
			    SELECTEDVALUE( dVendedores[Vendedor] ) 
			
			VAR vResultado = 
			    CALCULATE(
			        [Faturamento],
			        FILTER(
			            ALL(dVendedores[Vendedor], dVendedores[URL Foto]), 
			            dVendedores[Vendedor] <= vVendedor_contexto
			        )
			    )
			
			RETURN 
			    vResultado  
			```
		displayFolder: Bônus 5.9 Deneb\Gráfico Rosca
		lineageTag: e0685aca-f107-4d57-b27a-88f660fe6dd7

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Eixo_x = ```
			
			-- SIN( 2*PI() * ( [Acumulado]-[Faturamento] / 2) / [Fixo] )
			
			SIN(
			    2 * PI() * 
			    ( [Faturamento ACC] - [Faturamento] / 2 ) / 
			    [Faturamento Fixo]
			)
			```
		formatString: 0.0000%;-0.0000%;0.0000%
		displayFolder: Bônus 5.9 Deneb\Gráfico Rosca
		lineageTag: 8548703f-988f-4b2d-8002-136e69fa82b3

	measure Eixo_y = ```
			
			-- SIN( 2*PI() * ( [Acumulado]-[Faturamento] / 2) / [Fixo] )
			
			COS(
			    2 * PI() * 
			    ( [Faturamento ACC] - [Faturamento] / 2 ) / 
			    [Faturamento Fixo]
			)
			```
		formatString: 0.00
		displayFolder: Bônus 5.9 Deneb\Gráfico Rosca
		lineageTag: 4beb52a5-0137-4aee-8b9b-530ebf423418

	measure 'Variacao Cor' = ```
			
			
			VAR vPerc_yoy = [Faturamento YoY %]
			
			RETURN 
			    SWITCH(
			        TRUE(),
			        vPerc_yoy > 0, "#11c4d1",
			        vPerc_yoy < 0, "#bf5064",
			        "#0000"
			    )
			```
		displayFolder: Bônus 5.9 Deneb
		lineageTag: d9e5d4b4-170e-4f52-84bd-0bdf5f7996e6

	measure 'Faturamento Cartao Subtitulo' = ```
			
			
			VAR vMelhor_mes_tabela = 
			    TOPN( 
			        1,
			        ALL( dCalendario[Mes Abreviado], dCalendario[Nome do Mês], dCalendario[Mês] ),
			        [Faturamento],
			        DESC
			    )
			
			VAR vMelhor_mes_escalar = 
			    MAXX( vMelhor_mes_tabela, dCalendario[Mes Abreviado] )
			
			RETURN 
			    "(" & vMelhor_mes_escalar & ")" & " melhor mês" 
			```
		displayFolder: Cartões\Faturamento
		lineageTag: 2f82aa57-297c-4a65-8227-bf0169ccd9e7

	measure 'Faturamento Cartao Titulo' = ```
			
			
			VAR vPerc_ano_anterior = 
			    [Faturamento YoY %] 
			
			VAR vPerc_ano_anterior_formatado = 
			    FORMAT( vPerc_ano_anterior + 0 , "⮝ +0.0% vs Ano Anterior; ⮟ -0.0% vs Ano Anterior ; não houve variação" )
			
			RETURN 
			    vPerc_ano_anterior_formatado 
			```
		displayFolder: Cartões\Faturamento
		lineageTag: 928a5e04-2a4f-4162-97b0-b0cd211f6376

	measure 'Faturamento Cartao Titulo Form. Cond.' = ```
			
			
			VAR vPerc_ano_anterior = 
			    [Faturamento YoY %] 
			
			
			
			RETURN 
			    SWITCH(
			        TRUE(),
			        vPerc_ano_anterior > 0, "#53A365",
			        vPerc_ano_anterior < 0, "#CF6F45",
			        "#958FAB"
			    )
			```
		displayFolder: Cartões\Faturamento
		lineageTag: 44a73857-f152-4c79-b50a-2153e49962e1

	measure 'Resultado Cartão Subtitulo' = ```
			
			
			VAR vMelhor_mes_tabela = 
			    TOPN( 
			        1, 
			        ALL( dCalendario[Mes Abreviado], dCalendario[Nome do Mês], dCalendario[Mês]), 
			        [Resultado],
			        DESC 
			    )
			
			VAR vMelhor_mes_escalar = 
			    MAXX( vMelhor_mes_tabela, dCalendario[Mes Abreviado]  )
			
			
			RETURN 
			    "(" & vMelhor_mes_escalar & ")" & " melhor mês"
			```
		displayFolder: Cartões\Resultado
		lineageTag: 95ce681a-1e30-40e6-ab8e-90f02e711f1e

	measure 'Resultado Cartão Título' = ```
			
			
			VAR vPerc_yoy = [Resultado YoY %] 
			
			VAR vPerc_formatado = 
			    FORMAT( vPerc_yoy + 0 , "⮝ +0.0% vs Ano Anterior; ⮟ -0.0% vs Ano Anterior ; não houve variação" )
			
			VAR vIcone = 
			    SWITCH(
			        TRUE(),
			        vPerc_yoy > 0, "▲ ",
			        vPerc_yoy < 0, "▼ "
			    )
			
			RETURN 
			    vPerc_formatado  
			```
		displayFolder: Cartões\Resultado
		lineageTag: aeb690d0-8aad-4c17-b7c8-a4c241e86df3

	measure 'Resultado Cartão Título Form. Cond.' = ```
			
			
			VAR vPerc_yoy = [Resultado YoY %] 
			
			RETURN 
			    SWITCH( 
			        TRUE(), 
			        vPerc_yoy > 0, "#53A365",
			        vPerc_yoy < 0, "#CF6F45",
			        "#958fab"
			    )
			```
		displayFolder: Cartões\Resultado
		lineageTag: 92fd81b8-9e02-44f0-9922-31c85b9e3c4e

	measure 'Margem Cartão Subtitulo' = ```
			
			
			VAR vMelhor_mes_tabela = 
			    TOPN( 
			        1, 
			        ALL( dCalendario[Mes Abreviado], dCalendario[Nome do Mês], dCalendario[Mês]),
			        [Margem Percentual],
			        DESC 
			    )
			
			VAR vMelhor_mes_escalar = 
			    MAXX( vMelhor_mes_tabela, dCalendario[Mes Abreviado]  )
			
			
			RETURN 
			    "(" & vMelhor_mes_escalar & ")" & " melhor mês"
			```
		displayFolder: Cartões\Margem
		lineageTag: 109f4a54-0d50-4267-a22b-9664eeac3215

	measure 'Margem Cartão Título' = ```
			
			
			VAR vPerc_yoy = [Margem YoY %] 
			
			VAR vPerc_formatado = 
			    FORMAT( vPerc_yoy + 0 , "⮝ +0.0% vs Ano Anterior; ⮟ -0.0% vs Ano Anterior ; não houve variação" )
			
			
			RETURN 
			     vPerc_formatado 
			```
		displayFolder: Cartões\Margem
		lineageTag: 614f6820-6e8b-44ab-a2f3-9d7dbafd1894

	measure 'Margem Cartão Título Form. Cond.' = ```
			
			
			VAR vPerc_yoy = [Margem YoY %] 
			
			RETURN 
			    SWITCH( 
			        TRUE(), 
			        vPerc_yoy > 0, "#53A365",
			        vPerc_yoy < 0, "#CF6F45",
			        "#958fab"
			    )
			```
		displayFolder: Cartões\Margem
		lineageTag: 66f410ca-dea8-4c5a-a5be-198fa8d12131

	measure '001 Acumulado' = ```
			
			
			VAR vTabela = 
			    WINDOW(
			        1,ABS,
			        0, REL,
			        FILTER(
			            ALLSELECTED( dProdutos[Subcategoria] ),
			            [Faturamento]
			        ),
			        ORDERBY( [Faturamento], DESC )
			    )
			
			VAR vAcumulado = 
			    CALCULATE(
			        [Faturamento],
			        vTabela
			    )
			
			RETURN
			    vAcumulado  
			```
		formatString: "R$"\ #,0;-"R$"\ #,0;"R$"\ #,0
		displayFolder: Gráfico Pareto
		lineageTag: 29edea5a-14e1-4efb-82a3-2f7b91fe2307

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure '002 Percentual' = ```
			
			
			VAR vTabela = 
			    WINDOW(
			        1,ABS,
			        -1, ABS,
			        FILTER(
			            ALL( dProdutos[Subcategoria] ),
			            [Faturamento]
			        )
			    )
			
			VAR vValorFixo = 
			    CALCULATE(
			        [Faturamento],
			        vTabela
			    )
			
			VAR vPercentual = 
			    DIVIDE( [001 Acumulado], vValorFixo )
			
			RETURN
			    vPercentual    
			```
		formatString: #,0.0%;-#,0.0%;#,0.0%
		displayFolder: Gráfico Pareto
		lineageTag: 08f04142-9ebc-4977-9281-6c829c1bc3df

	measure '003 Destaque' = ```
			
			
			VAR vCorte = 
			    [Pareto Corte] 
			
			VAR vTabela = 
			    WINDOW(
			        1,ABS,
			        -1, ABS,
			        FILTER(
			            ALLSELECTED( dProdutos[Subcategoria] ),
			            [Faturamento] && 
			            [002 Percentual] <= vCorte 
			        )
			    )
			
			VAR vUltimoContexto = 
			    INDEX(
			        -1,
			        vTabela,
			        ORDERBY( [Faturamento], DESC )
			    )
			
			VAR vContexto = 
			    SELECTEDVALUE( dProdutos[Subcategoria] ) 
			
			VAR vResultado = 
			-- Precisa Retornar o Valor 1, para funcionar no gráfico que eu vou disponibilizar o template 
			    IF(
			        vContexto = vUltimoContexto,
			        1
			    )
			
			
			
			RETURN
			    vResultado       
			```
		formatString: 0
		displayFolder: Gráfico Pareto
		lineageTag: bf4e5235-7624-4f4e-a468-c252bbf86307

	measure '004 Classe ABC' = ```
			
			
			VAR vValor = [Faturamento]
			
			VAR vPercentual = [002 Percentual]
			
			VAR vCorte = [Pareto Corte] 
			
			VAR vClassificacao = 
			    SWITCH(
			        TRUE(),
			        ISBLANK( vValor ),                              BLANK(),
			        vPercentual <= vCorte,                          "A", 
			        vPercentual > vCorte && vPercentual <= 0.92,    "B",
			                                                        "C"
			    )
			
			VAR vResultado = 
			    IF(
			        ISINSCOPE( dProdutos[Subcategoria] ),
			         vClassificacao
			    )
			
			RETURN
			    vResultado  
			
			
			```
		displayFolder: Gráfico Pareto
		lineageTag: 59089545-efe6-49cb-89ca-bc10087e121e

	measure '005 Classe ABC Form. Cond' = ```
			
			
			
			--------------------------
			-- Cores
			--------------------------
			
			VAR vCorA = "#7A8B9F" 
			
			VAR vCorB = "#E1A55D" 
			
			VAR vCorC = "#CF6F45" 
			
			
			
			--------------------------
			-- Medidas
			--------------------------
			
			VAR vValor = [Faturamento]
			
			VAR vPercentual = [002 Percentual]
			
			VAR vCorte = [Pareto Corte] 
			
			VAR vClassificacao = 
			    SWITCH(
			        TRUE(),
			        ISBLANK( vValor ),                              BLANK(),
			        vPercentual <= vCorte,                          vCorA, 
			        vPercentual > vCorte && vPercentual <= 0.92,    vCorB,
			                                                        vCorC
			    )
			
			VAR vResultado = 
			    IF(
			        ISINSCOPE( dProdutos[Subcategoria] ),
			         vClassificacao
			    )
			
			RETURN
			    vResultado  
			
			
			```
		displayFolder: Gráfico Pareto
		lineageTag: fcc7dd4e-143c-4f0f-9a02-dfc5e7a405bb

	measure '006 Rotulos' = ```
			
			
			VAR vSubcategoria = 
			    SELECTEDVALUE( dProdutos[Subcategoria] )
			
			VAR vValor = 
			    [Faturamento] 
			
			VAR vValorFormatado = 
			    SWITCH(
			        TRUE(),
			        ABS( vValor ) >= 1e9,  FORMAT( vValor, "R$ #,0,,,.00 Bi" ),
			        ABS( vValor ) >= 1e6,  FORMAT( vValor, "R$ #,0,,.00 Mi" ),
			        ABS( vValor ) >= 1e3,  FORMAT( vValor, "R$ #,0,.00 K" ),
			        FORMAT( vValor, "R$ #,0" )
			    )
			  
			
			VAR vRotulo = 
			    IF(
			        vValor,
			        vSubcategoria  & " | " & vValorFormatado 
			    )
			
			RETURN
			    vRotulo    
			```
		displayFolder: Gráfico Pareto
		lineageTag: 6ba5de60-6226-4386-a7d7-25401a0d68ff

	measure '007 Subtitulo' = ```
			
			
			VAR vCorte = 
			    [Pareto Corte] 
			
			VAR vTabela = 
			    WINDOW(
			        1,ABS,
			        -1, ABS,
			        FILTER(
			            ALLSELECTED( dProdutos[Subcategoria] ),
			            [Faturamento] && 
			            [002 Percentual] <= vCorte 
			        )
			    )
			
			VAR vQtdeProdutos = 
			    COUNTROWS( vTabela )  
			
			VAR vPercentual = 
			    CALCULATE( [002 Percentual], vTabela )
			 
			VAR vPercentualFormatado = 
			    FORMAT( vPercentual, "#.0%" )
			
			VAR vValor = 
			    CALCULATE( [Faturamento], vTabela ) 
			
			VAR vValorFormatado = 
			    SWITCH(
			        TRUE(),
			        ABS( vValor ) >= 1e9,  FORMAT( vValor, "R$ #,0,,,.00 Bi" ),
			        ABS( vValor ) >= 1e6,  FORMAT( vValor, "R$ #,0,,.00 Mi" ),
			        ABS( vValor ) >= 1e3,  FORMAT( vValor, "R$ #,0,.00 K" ),
			        FORMAT( vValor, "R$ #,0" )
			    )
			
			
			VAR vSubtitulo = 
			    vQtdeProdutos & " produtos representam " 
			    & vPercentualFormatado & " do faturamento com total de " 
			    & vValorFormatado  
			
			RETURN
			    vSubtitulo  
			```
		displayFolder: Gráfico Pareto
		lineageTag: 2cb8b625-a774-45c7-9812-f33c7d6b9901

	measure 'Semaforo Animado' = ```
			
			
			/* 
			
			Link da aula com todo passo a passo para contrução dessa medida (copiar a URL no seu navegador )
			https://powerbidashboardsanimados.club.hotmart.com/lesson/ROxpMQYxOD?role=OWNER&classHash=wx7W8RbYO2
			
			*/
			
			VAR vPorcentagem = [Porcentagem Meta]
			
			VAR vPos_circ_meta = 
			    SWITCH(
			        TRUE(),
			        vPorcentagem >= 1, 242,
			        vPorcentagem >= 0.81, 145,
			        48
			    )
			
			
			
			---------------------------------------------
			-- FORMATAÇÕES
			---------------------------------------------
			
			VAR vEstrutura_cor = "#202831"
			
			VAR vCirc_fundo_cor = "#4B5661"
			
			VAR vMeta_cor = 
			    SWITCH(
			        TRUE(),
			        vPorcentagem >= 1, "#53A365",
			        vPorcentagem >= 0.8, "#E1A55D",
			        "#CF6F45"
			    )
			
			VAR vAnimacao = "on" 
			-- Opções on / off 
			
			
			VAR vPrefixo = "data:image/svg+xml,"
			
			RETURN 
			
			vPrefixo & 
			
			"
			<svg  viewBox='80 0 200 350' fill='none' xmlns='http://www.w3.org/2000/svg'>
				
			    <defs>
			        <style>
			
			            
			
			            #circ_colorido {
			                " & IF( vAnimacao = "on", "animation", BLANK() ) & ": blink-1 1s infinite both;
			            }
			
			            @keyframes blink-1 {
			                0%,
			                50%,
			                100% {
			                    opacity: 1;
			                }
			                25%,
			                75% {
			                    opacity: 0;
			                }
			            }
			
			
			
			        </style>
			
			    </defs>
			    
			    
			    <g id='semaforo'>
					
			        
			        <path id='circ_fundo' fill-rule='evenodd' clip-rule='evenodd' d='M135 48C135 67.33 119.33 83 100 83C80.67 83 65 67.33 65 48C65 28.67 80.67 13 100 13C119.33 13 135 28.67 135 48ZM135 142C135 161.33 119.33 177 100 177C80.67 177 65 161.33 65 142C65 122.67 80.67 107 100 107C119.33 107 135 122.67 135 142ZM100 271C119.33 271 135 255.33 135 236C135 216.67 119.33 201 100 201C80.67 201 65 216.67 65 236C65 255.33 80.67 271 100 271Z' fill='" & vCirc_fundo_cor & "'/>
					
			        
			        <circle id='circ_colorido' cx='100' cy='" & vPos_circ_meta & "' r='35' fill='" & vMeta_cor& "'/>
					
			        
			        <path id='estrutura' fill-rule='evenodd' clip-rule='evenodd' d='M57.0048 48.2237L57.2774 91.4474L100.361 91.7209L143.444 91.9944V48.4972V5H100.088H56.7322L57.0048 48.2237ZM110.022 19.7432C121.046 22.739 130.128 35.6024 130.128 48.2227C130.128 64.9662 116.788 78.3493 100.1 78.3493C91.74 78.3493 85.1976 75.5851 79.1178 69.4866C72.3133 62.6599 69.8588 55.9118 70.5669 45.9792C71.1351 38.0198 73.1499 33.4019 78.5423 27.7016C86.9 18.8683 97.2098 16.2623 110.022 19.7432ZM20.5076 20.4558C20.7447 20.8875 27.4333 33.2657 35.3722 47.9618C43.311 62.6578 50.1939 75.1293 50.6692 75.6773C51.1444 76.2243 51.5329 63.8471 51.5329 48.1713V19.6699H35.8046C27.1545 19.6699 20.2705 20.024 20.5076 20.4558ZM149.711 47.6128C149.711 62.9806 149.891 75.3588 150.111 75.1188C150.802 74.3623 180 20.6779 180 20.1613C180 19.891 173.185 19.6699 164.856 19.6699H149.711V47.6128ZM92.1881 24.3747C80.0652 28.2779 72.2589 43.4602 76.021 55.8206C76.9036 58.7211 77.03 58.4727 77.6546 52.6257C79.3361 36.8902 90.5504 26.0775 105.322 25.9486C110.205 25.9056 111.174 25.651 109.71 24.7959C106.55 22.9528 97.2965 22.7296 92.1881 24.3747ZM56.7552 142.268V185.23H100.1H143.444V142.268V99.3063H100.1H56.7552V142.268ZM107.68 113.292C130.742 119.294 137.975 148.805 120.293 164.754C107.153 176.607 87.1768 174.742 76.4743 160.664C66.1395 147.069 68.8749 128.248 82.6658 118.073C87.0682 114.825 94.4837 112.156 99.6109 111.974C101.066 111.922 104.696 112.515 107.68 113.292ZM23.0665 119.477C24.7522 122.504 31.1202 134.173 37.2177 145.412C43.3152 156.65 49.0158 166.788 49.8858 167.94C51.3105 169.829 51.4703 167.263 51.4995 142.006L51.5329 113.976H35.767H20L23.0665 119.477ZM149.711 141.919C149.711 157.287 149.891 169.665 150.11 169.425C150.705 168.773 180 114.805 180 114.36C180 114.148 173.185 113.976 164.856 113.976H149.711V141.919ZM92.1463 118.566C79.6954 123.03 72.1837 136.915 75.9229 148.555L77.4373 153.27L77.5407 148.48C77.8843 132.552 90.048 120.367 105.788 120.184L112.633 120.104L108.978 118.691C104.308 116.887 96.98 116.833 92.1463 118.566ZM56.7552 236.55V279.489L75.8163 279.774L94.8775 280.06L95.1887 286.609C95.4927 293.01 95.4394 293.158 92.8387 293.158H90.1775V319.354V345.551H100.64H111.103L110.823 319.616L110.544 293.682L108.194 293.347C106.035 293.04 105.844 292.464 105.844 286.274V279.536H124.644H143.444V236.574V193.613H100.1H56.7552V236.55ZM113.859 209.897C120.526 213.17 127.13 220.872 128.928 227.471C132.943 242.216 126.856 256.727 113.772 263.604C106.594 267.376 94.0284 267.618 87.4452 264.112C80.8485 260.599 77.3057 257.173 74.0324 251.139C66.5134 237.281 70.8134 219.858 83.8179 211.486C93.1051 205.507 103.766 204.943 113.859 209.897ZM23.0863 213.784C24.7836 216.809 31.1484 228.48 37.2313 239.718C43.3142 250.956 49.0054 261.094 49.8785 262.247C51.3125 264.138 51.4692 261.616 51.4995 236.312L51.5329 208.282H35.767H20L23.0863 213.784ZM149.742 236.312C149.761 252.399 150.156 263.673 150.669 262.771C154.238 256.496 180 208.979 180 208.671C180 208.457 173.185 208.282 164.856 208.282H149.711L149.742 236.312ZM92.0147 212.853C83.2079 216.151 77.5177 222.652 75.4936 231.727C74.4899 236.223 74.5536 238.133 75.8487 242.39L77.4269 247.577L77.5355 242.786C77.8979 226.816 90.1054 214.61 105.788 214.538C111.263 214.512 112.238 214.276 110.662 213.357C107.501 211.515 96.3888 211.214 92.0147 212.853Z' fill='" & vEstrutura_cor & "'/>
				</g>
			</svg>
			
			"
			```
		displayFolder: Tooltips
		lineageTag: 4cdebc91-d1ec-4245-bcb8-f813d3bfbb55

	measure 'Novo repartimento' = ```
			
			    [Custo] + [Despesa] + [Impostos] + [Comissao]
			```
		formatString: "R$"\ #,0.###############;-"R$"\ #,0.###############;"R$"\ #,0.###############
		displayFolder: _Principais
		lineageTag: 28e58726-1697-45f4-8f86-b2282b0fdb7b

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	partition _Medidas-332540c9-11b5-4161-9242-ae9d094a8fc8 = m
		mode: import
		source =
				let
				    Fonte = Table.FromRows(Json.Document(Binary.Decompress(Binary.FromText("i44FAA==", BinaryEncoding.Base64), Compression.Deflate)), let _t = ((type nullable text) meta [Serialized.Text = true]) in type table [#"Coluna 1" = _t]),
				    #"Tipo Alterado" = Table.TransformColumnTypes(Fonte,{{"Coluna 1", type text}}),
				    #"Colunas Removidas" = Table.RemoveColumns(#"Tipo Alterado",{"Coluna 1"})
				in
				    #"Colunas Removidas"

	annotation PBI_ResultType = Table

